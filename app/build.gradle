apply plugin: 'com.android.application'

ext.applicationId = "tfm.uniovi.pirateseas"
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 19
ext.versionClassifier = "Beta"
ext.isSnapshot = false

ext.minimumSdkVersion = 23
ext.compileTargetVersion = 28

android {
    compileSdkVersion project.ext.compileTargetVersion
    defaultConfig {
        applicationId project.ext.applicationId
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.compileTargetVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.versionClassifier == null){
        if(ext.isSnapshot){
            versionName += "-SNAPSHOT"
        }
    } else {
        versionName = ext.versionClassifier + "-" + versionName
        if(ext.isSnapshot)
            versionName += "-SNAPSHOT"
    }

    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
